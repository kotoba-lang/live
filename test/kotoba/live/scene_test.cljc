(ns kotoba.live.scene-test
  (:require [clojure.test :refer [deftest is]]
            [kotoba.live.director :as director]
            [kotoba.live.scene :as scene]
            [kotoba.live.show :as show]))

(def scene-edn
  "{:dance/show   {:bpm 128.0 :stage :hall}
    :dance/avatar {:vrm \"m.vrm\" :scale 1.0}
    :dance/setlist [{:title \"A\" :bpm 128.0 :bars 4 :dance :wota
                     :cues [{:beat 1 :kind :drop :tag \"hook\"}]
                     :audio :opener}]
    :dance/lighting [{:fixture :front-par :intensity 0.8 :envelope :hold}]
    :dance/vj [{:pattern :stripes :palette :cool-wave}]
    :dance/triggers [{:on :drop :fx :confetti :sound :coin}]}")

(deftest from-edn-builds-a-runnable-scene
  (let [sc (scene/from-edn scene-edn)]
    (is (some? sc))
    (is (= 128.0 (get-in sc [:show :grid :bpm])))
    (is (= "m.vrm" (:vrm (:avatar sc))))))

(deftest non-map-root-returns-nil
  (is (nil? (scene/from-edn "[1 2 3]"))))

(deftest frame-produces-render-ir-and-sounds
  (let [sc0 (update (scene/from-edn scene-edn) :show show/start)]
    (loop [sc sc0 i 0 saw-sound false saw-fx false]
      (if (>= i 200)
        (do (is saw-sound "expected at least one synthesised sound")
            (is saw-fx "expected the drop trigger's confetti fx to fire"))
        (let [[sc' fr] (scene/frame sc (/ 1.0 30.0))]
          (recur sc' (inc i)
                 (or saw-sound (seq (:sounds fr)))
                 (or saw-fx (some #(= "confetti" (director/action % "fx")) (:actions fr)))))))))

(deftest run-headless-is-deterministic
  (let [sc1 (scene/from-edn scene-edn)
        sc2 (scene/from-edn scene-edn)
        r1 (scene/run-headless sc1 60 30.0)
        r2 (scene/run-headless sc2 60 30.0)]
    (is (= (:final-render-ir r1) (:final-render-ir r2)))
    (is (= (:fx-counts r1) (:fx-counts r2)))
    (is (= 60 (:frames r1)))
    (is (= 1 (:mesh-count r1)) "vrm bound -> exactly one mesh in the final frame")))

(deftest clip-names-collects-authored-clip-names
  (let [sc (scene/from-edn
            "{:dance/show {:bpm 120.0 :stage :hall}
              :dance/clips [{:name \"wave\" :tracks [{:bone \"hips\" :keys [{:t 0.0 :pos [0 0 0]}]}]}]
              :dance/setlist [{:title \"A\" :bars 4 :dance :wota :cues [{:beat 1 :kind :drop}]}]}")]
    (is (= ["wave"] (scene/clip-names sc)))))
