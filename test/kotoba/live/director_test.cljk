(ns kotoba.live.director-test
  (:require [clojure.test :refer [deftest is]]
            [kotoba.live.director :as director]
            [kotoba.live.scene :as scene]
            [kotoba.live.show :as show]))

(def scene-edn
  "{:dance/show {:bpm 140.0 :stage :festival}
    :dance/triggers
    [{:on :drop      :fx :confetti :sound :coin :camera :punch}
     {:on :callout   :tag \"intro\" :camera :closeup}
     {:on :phrase    :vj-cut true}
     {:on :bar :every 8 :fx :pyro}]
    :dance/setlist
    [{:title \"A\" :bpm 140.0 :bars 16 :dance :wota
      :cues [{:beat 0 :kind :callout :tag \"intro\"}
             {:beat 16 :kind :drop :tag \"hook\"}]}]}")

(deftest parses-triggers-with-freeform-actions
  (let [sc (scene/from-edn scene-edn)]
    (is (= 4 (count (:triggers (:director sc)))))
    (let [drop (first (:triggers (:director sc)))]
      (is (= :drop (:on drop)))
      (is (= "confetti" (director/action drop "fx")))
      (is (= "coin" (director/action drop "sound")))
      (is (= "punch" (director/action drop "camera"))))))

(deftest resolves-drop-event-to-confetti
  (let [sc0 (update (scene/from-edn scene-edn) :show show/start)]
    (loop [sc sc0 i 0]
      (if (>= i 600)
        (is false "drop cue never resolved to confetti")
        (let [[sc' fr] (scene/frame sc (/ 1.0 30.0))]
          (if (some #(= "confetti" (director/action % "fx")) (:actions fr))
            (is true)
            (recur sc' (inc i))))))))

(deftest tag-filter-scopes-callout
  (let [sc (scene/from-edn scene-edn)
        mk (fn [tag] {:type :cue :track-index 0 :cue {:at-beat 0 :kind :callout :tag tag}})]
    (is (= 1 (count (director/resolve-event (:director sc) (mk "intro")))))
    (is (= 0 (count (director/resolve-event (:director sc) (mk "other")))))))

(deftest every-filter-gates-periodic-bars
  (let [sc (scene/from-edn scene-edn)
        bar (fn [i] {:type :beat :beat {:type :bar :time 0.0 :bar-index i}})]
    (is (= 1 (count (director/resolve-event (:director sc) (bar 8)))))
    (is (= 0 (count (director/resolve-event (:director sc) (bar 7)))))))
