(ns kotoba.live.crowd-test
  (:require [clojure.test :refer [deftest is]]
            [kotoba.live.crowd :as crowd]
            [kotoba.live.stage :as stage]))

(deftest deterministic-layout
  (let [s (stage/build-preset :hall)
        cfg {:fans-target 200 :cap 4096 :pit-bias 0.5 :seed 42}
        a (crowd/new-crowd cfg s)
        b (crowd/new-crowd cfg s)]
    (is (= (count (:fans a)) (count (:fans b))))
    (doseq [[fa fb] (map vector (:fans a) (:fans b))]
      (is (< (reduce + (map #(Math/abs (double (- %1 %2))) (:home fa) (:home fb))) 1e-5)))))

(deftest cap-clamps-fan-count
  (let [s (stage/build-preset :festival)
        c (crowd/new-crowd {:fans-target 99999 :cap 100 :pit-bias 0.5 :seed 1} s)]
    (is (= 100 (count (:fans c))))))

(deftest snapshot-height-varies-in-jump-mood
  (let [s (stage/build-preset :club)
        c (crowd/set-mood-all (crowd/new-crowd {:fans-target 30 :cap 4096 :pit-bias 0.65 :seed 1} s) :jump)
        phase {:time 0.0 :beat 0 :bar 0 :phrase 0 :beat-frac 0.1 :bar-frac 0.025}
        [_ snap] (crowd/snapshot c phase)
        max-h (apply max (map #(nth (:position %) 1) snap))]
    (is (> max-h 0.0))))

(deftest react-pit-only-pumps-pit-fans
  (let [s (stage/build-preset :hall)
        c (crowd/new-crowd (crowd/default-config) s)
        pit (stage/zone s :pit)
        before (mapv :energy (:fans c))
        c2 (crowd/react c :yell (fn [p] (stage/box-contains? pit p)))
        after (mapv :energy (:fans c2))
        bumped (count (keep-indexed (fn [i f] (when (and (stage/box-contains? pit (:home f)) (> (nth after i) (nth before i))) i))
                                     (:fans c)))]
    (is (> bumped 0))))
