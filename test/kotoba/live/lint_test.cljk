(ns kotoba.live.lint-test
  (:require [clojure.test :refer [deftest is]]
            [kotoba.live.lint :as lint]))

(deftest flags-unknown-wave-and-vowel
  (let [src "{:dance/audio  {:bank {:kick {:wave \"sino\" :freq 100}}}
              :dance/avatar {:vrm \"a.vrm\" :voice {:phonemes [{:at-beat 0 :vowel :x :dur 0.5}]}}
              :dance/setlist [{:title \"A\" :bars 8 :dance :wota :cues [{:beat 1 :kind :drop}]}]}"
        lints (lint/lint-scene src)]
    (is (some #(= "dance/audio.bank.kick.wave" (:path %)) lints))
    (is (some #(= "dance/avatar.voice.phonemes[0].vowel" (:path %)) lints))))

(deftest flags-unknown-expression-source-and-bad-fov
  (let [src "{:dance/avatar {:vrm \"a.vrm\" :expressions {:happy {:from :loudness}}}
              :dance/camera {:fov 9.0}
              :dance/setlist [{:title \"A\" :bars 8 :dance :wota :cues [{:beat 1 :kind :drop}]}]}"
        lints (lint/lint-scene src)]
    (is (some #(= "dance/avatar.expressions.happy" (:path %)) lints))
    (is (some #(= "dance/camera.fov" (:path %)) lints))))

(deftest clean-scene-has-no-lints
  (let [src "{:dance/show     {:bpm 128.0 :stage :hall :swing 0.1}
              :dance/lighting [{:fixture :front-par :intensity 0.8 :envelope :hold}]
              :dance/vj       [{:pattern :stripes :palette :cool-wave}]
              :dance/triggers [{:on :drop :fx :confetti}]
              :dance/setlist  [{:title \"A\" :bars 8 :dance :wota
                                :cues [{:beat 1 :kind :drop :tag \"hook\"}]}]}"]
    (is (empty? (lint/lint-scene src)))))

(deftest catches-unknown-enums
  (let [src "{:dance/show    {:bpm 120.0 :stage :halll}
              :dance/setlist [{:title \"A\" :bars 8 :dance :wotaa
                               :cues [{:beat 0 :kind :drrop :tag \"x\"}]}]}"
        lints (lint/lint-scene src)
        paths (set (map :path lints))]
    (is (contains? paths "dance/show.stage"))
    (is (contains? paths "dance/setlist[0].dance"))
    (is (contains? paths "dance/setlist[0].cues[0].kind"))))

(deftest catches-range-and-empty-and-bpm
  (let [src "{:dance/show    {:bpm 0 :stage :hall :swing 1.5}
              :dance/lighting [{:fixture :spot :intensity 2.0}]
              :dance/setlist []}"
        lints (lint/lint-scene src)
        paths (set (map :path lints))]
    (is (contains? paths "dance/show.bpm"))
    (is (contains? paths "dance/show.swing"))
    (is (contains? paths "dance/lighting[0].intensity"))
    (is (contains? paths "dance/setlist"))))

(deftest catches-dangling-trigger-tag-and-bad-on
  (let [src "{:dance/show     {:bpm 120.0 :stage :hall}
              :dance/triggers [{:on :drop :tag \"nope\" :fx :x}
                               {:on :wiggle :fx :y}]
              :dance/setlist  [{:title \"A\" :bars 8 :dance :wota
                                :cues [{:beat 0 :kind :drop :tag \"real\"}]}]}"
        lints (lint/lint-scene src)
        paths (set (map :path lints))]
    (is (contains? paths "dance/triggers[0].tag"))
    (is (contains? paths "dance/triggers[1].on"))))

(deftest catches-unknown-post-fx
  (let [src "{:dance/show   {:bpm 120.0 :stage :hall}
              :dance/post   [{:fx :bloom :intensity 0.5} {:fx :sparkle-blast} {:intensity 1.0}]
              :dance/setlist [{:title \"A\" :bars 8 :dance :wota
                               :cues [{:beat 1 :kind :drop :tag \"hook\"}]}]}"
        lints (lint/lint-scene src)
        paths (set (map :path lints))]
    (is (contains? paths "dance/post[1].effect"))
    (is (contains? paths "dance/post[2].effect"))
    (is (not (contains? paths "dance/post[0].effect")))))

(deftest catches-beat-zero-cue
  (let [src "{:dance/show    {:bpm 120.0 :stage :hall}
              :dance/setlist [{:title \"A\" :bars 8 :dance :wota
                               :cues [{:beat 0 :kind :drop :tag \"hook\"}]}]}"
        lints (lint/lint-scene src)]
    (is (some #(= "dance/setlist[0].cues[0].beat" (:path %)) lints))))

(deftest catches-unbound-avatar-and-live2d
  (let [src "{:dance/show   {:bpm 120.0 :stage :hall}
              :dance/avatar {:scale -1.0}
              :dance/live2d {:lipsync :ParamMouthOpenY
                             :motions [{:name \"idle\"}]}
              :dance/setlist [{:title \"A\" :bars 8 :dance :wota
                               :cues [{:beat 1 :kind :drop :tag \"hook\"}]}]}"
        lints (lint/lint-scene src)
        paths (set (map :path lints))]
    (is (contains? paths "dance/avatar.vrm"))
    (is (contains? paths "dance/avatar.scale"))
    (is (contains? paths "dance/live2d.model"))
    (is (contains? paths "dance/live2d.motions[0]"))))

(deftest catches-dangling-live2d-motion-ref
  (let [src "{:dance/show   {:bpm 120.0 :stage :hall}
              :dance/live2d {:model \"m\" :motion \"missing\"
                             :motions [{:name \"wave\" :keys [{:t 0.0 :params {:ParamArmL 0.0}}]}]}
              :dance/setlist [{:title \"A\" :bars 8 :dance :wota
                               :cues [{:beat 1 :kind :drop :tag \"hook\"}]}]}"]
    (is (some #(= "dance/live2d.motion" (:path %)) (lint/lint-scene src)))))

(deftest catches-bad-clips-and-dangling-clip-ref
  (let [src "{:dance/show   {:bpm 120.0 :stage :hall}
              :dance/avatar {:vrm \"m.vrm\" :clip \"missing\"}
              :dance/clips  [{:name \"wave\" :tracks [{:bone \"hips\" :keys [{:t 0.0 :pos [0 0 0]}]}]}
                             {:tracks []}]
              :dance/setlist [{:title \"A\" :bars 8 :dance :wota
                               :cues [{:beat 1 :kind :drop :tag \"hook\"}]}]}"
        lints (lint/lint-scene src)
        paths (set (map :path lints))]
    (is (contains? paths "dance/clips[1].name"))
    (is (contains? paths "dance/clips[1].tracks"))
    (is (contains? paths "dance/avatar.clip"))))

(deftest valid-clip-and-ref-are-clean
  (let [src "{:dance/show   {:bpm 120.0 :stage :hall}
              :dance/avatar {:vrm \"m.vrm\" :clip \"wave\"}
              :dance/clips  [{:name \"wave\" :tracks [{:bone \"hips\" :keys [{:t 0.0 :pos [0 0 0]}]}]}]
              :dance/setlist [{:title \"A\" :bars 8 :dance :wota
                               :cues [{:beat 1 :kind :drop :tag \"hook\"}]}]}"]
    (is (empty? (lint/lint-scene src)))))

(deftest bound-avatar-and-live2d-are-clean
  (let [src "{:dance/show   {:bpm 120.0 :stage :hall}
              :dance/avatar {:vrm \"m.vrm\" :scale 1.0}
              :dance/live2d {:model \"haru.model3.json\" :motions [{:name \"idle\" :file \"idle.motion3.json\"}]}
              :dance/setlist [{:title \"A\" :bars 8 :dance :wota
                               :cues [{:beat 1 :kind :drop :tag \"hook\"}]}]}"]
    (is (empty? (lint/lint-scene src)))))

(deftest non-map-root-is-an-error
  (let [lints (lint/lint-scene "[1 2 3]")]
    (is (= 1 (count lints)))
    (is (= :error (:severity (first lints))))))
