(ns kotoba.live.setlist-test
  (:require [clojure.test :refer [deftest is]]
            [kotoba.live.setlist :as setlist]))

(defn- t [id bpm beats cues]
  {:id id :title (str "track-" id) :bpm bpm :length-beats beats
   :cues (mapv (fn [[b k]] {:at-beat b :kind k :tag ""}) cues)
   :dance nil :audio nil})

(deftest locate-returns-correct-track
  (let [s (reduce setlist/push (setlist/new-setlist)
                   [(t 1 120.0 64 []) (t 2 120.0 64 []) (t 3 60.0 64 [])])]
    (is (= 0 (first (setlist/locate s 0.0))))
    (is (= 0 (first (setlist/locate s 31.9))))
    (is (= 1 (first (setlist/locate s 32.0))))
    (is (= 1 (first (setlist/locate s 63.0))))
    (is (= 2 (first (setlist/locate s 64.5))))
    (is (nil? (setlist/locate s 999.0)))))

(deftest cues-between-is-open-closed
  (let [s (setlist/push (setlist/new-setlist)
                         (t 1 120.0 128 [[16 :drop] [32 :breakdown] [48 :drop]]))]
    (is (= 1 (count (setlist/cues-between s 0 0 16))))
    (is (= 16 (:at-beat (first (setlist/cues-between s 0 0 16)))))
    (is (= 2 (count (setlist/cues-between s 0 16 48))))))

(deftest cues-sorted-after-push
  (let [s (setlist/push (setlist/new-setlist)
                         (t 1 120.0 128 [[48 :drop] [16 :drop] [32 :breakdown]]))]
    (is (= [16 32 48] (mapv :at-beat (:cues (first (:tracks s))))))))
