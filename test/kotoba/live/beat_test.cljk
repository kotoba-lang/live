(ns kotoba.live.beat-test
  (:require [clojure.test :refer [deftest is]]
            [kotoba.live.beat :as beat]))

(deftest beat-seconds-test
  (is (< (Math/abs (- (beat/beat-seconds 120.0) 0.5)) 1e-6)))

(deftest tick-emits-one-beat-per-500ms-at-120
  (let [g (beat/new-grid 120.0)
        [_ evts] (beat/tick g 0.5)]
    (is (= :eighth (:type (first evts))))
    (is (some #(and (= :beat (:type %)) (= 1 (:beat-index %))) evts))))

(deftest bar-and-phrase-fire-on-boundary
  (let [g (beat/new-grid 120.0)
        [_ evts] (beat/tick g 16.0)
        bars (count (filter #(= :bar (:type %)) evts))
        phrases (count (filter #(= :phrase (:type %)) evts))]
    (is (= 8 bars))
    (is (= 1 phrases))))

(deftest deterministic-replay
  (let [a (beat/new-grid 128.0)
        b (beat/new-grid 128.0)
        step (fn [g] (first (beat/tick g (/ 1.0 60.0))))
        a' (reduce (fn [g _] (step g)) a (range 600))
        b' (reduce (fn [g _] (step g)) b (range 600))]
    (is (= (:beat (beat/phase a')) (:beat (beat/phase b'))))
    (is (< (Math/abs (- (:bar-frac (beat/phase a')) (:bar-frac (beat/phase b')))) 1e-5))))

(deftest swing-shifts-offbeat-eighths
  (let [straight (beat/new-grid 120.0)
        swung (beat/new-grid 120.0 :swing 0.25)
        [_ se] (beat/tick straight 0.51)
        [_ we] (beat/tick swung 0.51)
        find-e1 (fn [evs] (:time (first (filter #(and (= :eighth (:type %)) (= 1 (:eighth-index %))) evs))))
        s-e (find-e1 se)
        w-e (find-e1 we)]
    (is (some? s-e))
    (is (some? w-e))
    (is (> w-e s-e))))

(deftest rewind-resets-time
  (let [g (beat/new-grid 120.0)
        [g' _] (beat/tick g 5.0)
        g'' (beat/rewind g')]
    (is (zero? (:beat (beat/phase g''))))))
