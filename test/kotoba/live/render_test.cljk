(ns kotoba.live.render-test
  (:require [clojure.test :refer [deftest is]]
            [kotoba.live.camera :as camera]
            [kotoba.live.render :as render]
            [kotoba.live.scene :as scene]
            [kotoba.live.show :as show]))

(def scene-edn
  "{:dance/show   {:bpm 120.0 :stage :club}
    :dance/avatar {:vrm \"m.vrm\" :home [0.0 1.0 0.0] :scale 1.0}
    :dance/crowd  {:fans 40 :seed 3}
    :dance/vj     [{:pattern :stripes :palette :neon-pink}]
    :dance/setlist [{:title \"A\" :bpm 120.0 :bars 8 :dance :wota
                     :cues [{:beat 0 :kind :drop :tag \"d\"}]}]}")

(defn- snap-after [secs]
  (let [sc0 (update (scene/from-edn scene-edn) :show show/start)
        steps (int (/ secs (/ 1.0 60.0)))
        sh (reduce (fn [sh _] (first (show/tick sh (/ 1.0 60.0)))) (:show sc0) (range steps))
        [_ snap] (show/snapshot sh)]
    [snap (:avatar sc0)]))

(deftest render-ir-is-well-formed
  (let [[snap avatar] (snap-after 1.0)
        ir (render/show->render-ir snap avatar (camera/default-rig) [])]
    (is (map? (:globals ir)))
    (is (>= (count (:instances ir)) 1))
    (let [perf (first (:instances ir))]
      (is (> (nth (:pos perf) 1) 0.5)))))

(deftest camera-tracks-performer
  (let [[snap avatar] (snap-after 0.5)
        ir (render/show->render-ir snap avatar (camera/default-rig) [])
        eye (get-in ir [:globals :eye])
        target (get-in ir [:globals :target])]
    (is (> (nth eye 2) (nth target 2)))
    (is (> (nth eye 1) (nth target 1)))))

(deftest emits-vrm-avatar-mesh-and-lights
  (let [[snap avatar] (snap-after 0.2)
        ir (render/show->render-ir snap avatar (camera/default-rig) [])]
    (is (= 1 (count (:meshes ir))))
    (let [m (first (:meshes ir))]
      (is (= "m.vrm" (:url m)))
      (is (= :rig (:skin m)))
      (is (contains? (:expressions m) :aa))
      (is (contains? (:expressions m) :blink)))
    (is (seq (:lights ir)))
    (is (map? (:camera ir)))
    (is (= :mtoon (:model (first (:materials ir)))))))

(deftest emits-animation-layer-for-avatar-clip
  (let [[snap avatar] (snap-after 0.5)
        avatar (assoc avatar :clip "idle")
        ir (render/show->render-ir snap avatar (camera/default-rig) [])]
    (is (= 1 (count (:animations ir))))
    (let [a (first (:animations ir))]
      (is (= "idle" (:clip a)))
      (is (= :performer (:target a)))
      (is (some? (:time a))))))

(deftest no-animation-layer-without-clip
  (let [[snap avatar] (snap-after 0.2)
        ir (render/show->render-ir snap avatar (camera/default-rig) [])]
    (is (empty? (:animations ir)))))

(deftest no-mesh-when-avatar-unbound
  (let [[snap avatar] (snap-after 0.2)
        avatar (assoc avatar :vrm "")
        ir (render/show->render-ir snap avatar (camera/default-rig) [])]
    (is (empty? (:meshes ir)))))

(deftest deterministic-render-ir
  (let [a (let [[snap avatar] (snap-after 0.75)] (render/show->render-ir snap avatar (camera/default-rig) []))
        b (let [[snap avatar] (snap-after 0.75)] (render/show->render-ir snap avatar (camera/default-rig) []))]
    (is (= a b))))
